plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(19)
    }
}

repositories {
    mavenCentral().content {
        excludeModule("javax.media", "jai_core")
    }

    maven {
        url 'https://repo.osgeo.org/repository/geotools-releases/'
        name 'OSGeo Release Repository'
    }
}

dependencies {
    implementation 'io.javalin:javalin:5.6.3'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'org.slf4j:slf4j-simple:2.0.6'
    implementation 'org.json:json:20230227'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'com.jhlabs:filters:2.0.235'
    implementation 'com.squareup.okhttp3:okhttp:4.9.1'
    implementation 'io.github.coordinates2country:coordinates2country:1.2'
    implementation 'org.geotools:gt-shapefile:30.0'
    implementation 'org.locationtech.jts:jts:1.19.0'
    implementation 'io.github.cdimascio:dotenv-java:3.0.0'

    annotationProcessor group: "org.projectlombok", name: "lombok", version: "1.18.30"
    compileOnly group: "org.projectlombok", name: "lombok", version: "1.18.30"
}

tasks.register('execute', JavaExec) {
    setMainClass('dev.turtywurty.turtyapi.TurtyAPI')
    args 'dev'
    classpath = sourceSets.main.runtimeClasspath
}

jar {
    manifest {
        attributes(
                "Implementation-Title": "TurtyAPI",
                "Implementation-Version": "1.0-alpha",
                "Main-Class": "dev.turtywurty.turtyapi.TurtyAPI"
        )
    }
}

shadowJar {
    zip64 true

    mergeServiceFiles()
    minimize()
}

compileJava.options.encoding = "UTF-8"

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}